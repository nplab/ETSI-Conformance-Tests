#!/usr/bin/env tcsh

#
# Copyright (c) 2015 Michael Tuexen
# All rights reserved.
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions
# are met:
# 1. Redistributions of source code must retain the above copyright
#    notice, this list of conditions and the following disclaimer.
# 2. Redistributions in binary form must reproduce the above copyright
#    notice, this list of conditions and the following disclaimer in the
#    documentation and/or other materials provided with the distribution.
#
# THIS SOFTWARE IS PROVIDED BY THE AUTHOR AND CONTRIBUTORS ``AS IS'' AND
# ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
# IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
# ARE DISCLAIMED.  IN NO EVENT SHALL THE AUTHOR OR CONTRIBUTORS BE LIABLE
# FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL
# DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS
# OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION)
# HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT
# LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY
# OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF
# SUCH DAMAGE.
#

set packetdrill = /home/tuexen/packetdrill/gtests/net/packetdrill/packetdrill
set delay = 1
set testcases = (sctp-at-tests/sctp-at-v-2-2   \
                 sctp-at-tests/sctp-at-i-2-3   \
                 sctp-at-tests/sctp-at-i-2-4   \
                 sctp-at-tests/sctp-at-i-2-5   \
                 sctp-at-tests/sctp-at-v-2-6   \
                 sctp-at-tests/sctp-at-i-2-7-1 \
                 sctp-at-tests/sctp-at-i-2-7-2 \
                 sctp-at-tests/sctp-at-i-2-7-3 \
                 sctp-at-tests/sctp-at-i-2-7-4 \
                 sctp-at-tests/sctp-at-i-2-8   \
                 sctp-at-tests/sctp-at-i-2-9   \
                 sctp-at-tests/sctp-at-i-2-10  \
                 sctp-at-tests/sctp-at-i-2-11  \
                 sctp-at-tests/sctp-at-i-2-12  \
                 sctp-at-tests/sctp-at-i-2-13  \
                 sctp-at-tests/sctp-at-v-2-14  \
                 sctp-e-tests/sctp-e-o-6-1     \
                 sctp-e-tests/sctp-e-o-6-2     \
                 sctp-e-tests/sctp-e-i-6-3     \
                 sctp-e-tests/sctp-e-i-6-4     \
                 sctp-e-tests/sctp-e-i-6-5     \
                 sctp-e-tests/sctp-e-i-6-6     \
                 sctp-bdc-tests/sctp-bdc-i-7-1 \
                 sctp-bdc-tests/sctp-bdc-i-7-2 \
                 sctp-bdc-tests/sctp-bdc-i-7-3 \
                 sctp-bdc-tests/sctp-bdc-v-7-4 \
                 sctp-bdc-tests/sctp-bdc-v-7-5 \
                 sctp-bdc-tests/sctp-bdc-v-7-6 \
                 sctp-bdc-tests/sctp-bdc-v-7-7 \
                 sctp-bdc-tests/sctp-bdc-v-7-8 \
                 sctp-a-tests/sctp-a-v-9-1     \
                 sctp-a-tests/sctp-a-v-9-2     \
                 sctp-a-tests/sctp-a-o-9-3     \
                 sctp-m-tests/sctp-m-i-10-1    \
                 sctp-m-tests/sctp-m-i-10-2    \
                 sctp-m-tests/sctp-m-i-10-3    \
                 sctp-m-tests/sctp-m-i-10-4    )

set rootdir = `/usr/bin/dirname $0`
set os = `uname -s | tr '[A-Z]' '[a-z]'`
set passed = 0
set failed = 0
set run = 0

printf "Name                                    Verdict\n"
printf "===============================================\n"
foreach testcase ($testcases)
  printf "%-40.40s " `/usr/bin/basename $testcase`
  if (-f ${rootdir}/${testcase}-$os.pkt) then
    $packetdrill ${rootdir}/${testcase}-$os.pkt >& /dev/null
    set result = $status
  else
    $packetdrill ${rootdir}/${testcase}.pkt >& /dev/null
    set result = $status
  endif
  if ($result == 0) then
    @ passed++
    printf "\033[32m%-6.6s\033[0m\n" "PASSED"
  else
    @ failed++
    printf "\033[31m%-6.6s\033[0m\n" "FAILED"
  endif
  @ run++
  sleep $delay
end
printf "===============================================\n"
printf "Summary: Number of tests run:    %3u\n" $run
printf "         Number of tests passed: %3u\n" $passed
printf "         Number of tests failed: %3u\n" $failed

